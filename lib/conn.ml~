type t = {
  conn : Websocket_lwt_unix.conn;
  open_requests : Jsonrpc.Response.t Lwt.u String.Table.t;
}

let read (t : t) : Jsonrpc.Packet.t Lwt.t =
  let%lwt (frame : Websocket.Frame.t) = Websocket_lwt_unix.read t.conn in
  Lwt.return
    (frame.Websocket.Frame.content |> Yojson.Safe.from_string
   |> Jsonrpc.Packet.t_of_yojson)

let respond (t : t) (packet : Jsonrpc.Response.t) : unit Lwt.t =
  let content = Yojson.Safe.to_string (Jsonrpc.Response.yojson_of_t packet) in
  let frame = Websocket.Frame.create ~content () in
  Websocket_lwt_unix.write t.conn frame

let connect ~(url : string) ~on_notification ~on_request : t Lwt.t =
  let uri : Uri.t = Uri.of_string url in
  let%lwt (endp : Conduit.endp) =
    Resolver_lwt.resolve_uri ~uri Resolver_lwt_unix.system
  in
  let ctx : Conduit_lwt_unix.ctx = Lazy.force Conduit_lwt_unix.default_ctx in
  let%lwt (client : Conduit_lwt_unix.client) =
    Conduit_lwt_unix.endp_to_client ~ctx endp
  in
  let%lwt conn = Websocket_lwt_unix.connect client uri in
  let open_requests = String.Table.create () in
  let t = { conn; open_requests } in
  let () =
    let rec loop () =
      match%lwt read t with
      | Jsonrpc.Packet.Notification notification ->
          let%lwt () = on_notification notification in
          loop ()
      | Response ({ id = `String id; _ } as response) ->
          (match Hashtbl.find_and_remove t.open_requests id with
          | None ->
              Printf.eprintf "BUG! Received response for unknown request: %s"
                (response |> Jsonrpc.Response.yojson_of_t
               |> Yojson.Safe.to_string)
          | Some resolver -> Lwt.wakeup resolver response);
          loop ()
      | Response ({ id = `Int _; _ } as response) ->
          Printf.eprintf "BUG! Received response for unknown request: %s"
            (response |> Jsonrpc.Response.yojson_of_t |> Yojson.Safe.to_string);
          loop ()
      | Request request ->
          let%lwt response = on_request request in
          let%lwt () = respond t response in
          loop ()
      | (Batch_response _ | Batch_call _) as malformed ->
          Printf.eprintf "Unsupported Batch from server: %s\n"
            (malformed |> Jsonrpc.Packet.yojson_of_t |> Yojson.Safe.to_string);
          loop ()
    in
    ignore (loop ())
  in
  Lwt.return t

let id_exn (packet : Jsonrpc.Request.t) =
  match packet.id with
  | `String str -> str
  | `Int _ -> failwith "Cannot create int id"

let request (t : t) (packet : Jsonrpc.Request.t) : Jsonrpc.Response.t Lwt.t =
  let promise, resolver = Lwt.wait () in
  Hashtbl.set t.open_requests ~key:(id_exn packet) ~data:resolver;
  let content = Yojson.Safe.to_string (Jsonrpc.Request.yojson_of_t packet) in
  let frame = Websocket.Frame.create ~content () in
  let%lwt () = Websocket_lwt_unix.write t.conn frame in
  promise

let create_id () =
  let rand = List.permute (List.init 20 ~f:Fn.id) in
  "REQ" ^ String.concat (List.map rand ~f:Int.to_string)
